
struct UnitPresenter {
  domino &!GameToDominoConnection;
  unit &!Unit;

  unitViewId! i64;
}

func UnitPresenter(
  domino &!GameToDominoConnection,
  unit &!Unit)
UnitPresenter {
  unitViewId =
      domino!.CreateUnit(
          if unit.isPlayer {
            InitialUnit(
              unit.location,
              InitialSymbol(
                  InitialSymbolGlyph(
                      SymbolId("AthSymbols", 106),
                      ConstantVec4iAnimation(Vec4i(0, 96, 96, 255))),
                  Some(
                      InitialSymbolOutline(
                          CenteredOutline(),
                          ConstantVec4iAnimation(Vec4i(0, 128, 128, 255)))),
                  None<InitialSymbolSides>(),
                  0,
                  100),
              InitialSymbol(
                  InitialSymbolGlyph(
                      SymbolId("AthSymbols", 57),
                      ConstantVec4iAnimation(Vec4i(255, 255, 255, 380))),
                  None<InitialSymbolOutline>(),
                  None<InitialSymbolSides>(),
                  0,
                  100),
              List<(i64, InitialSymbol)>(),
              unit.hp,
              unit.maxHp)
          } else {
            InitialUnit(
              unit.location,
              InitialSymbol(
                  InitialSymbolGlyph(
                      SymbolId("AthSymbols", 123),
                      ConstantVec4iAnimation(Vec4i(128, 0, 0, 255))),
                  Some(
                      InitialSymbolOutline(
                          CenteredOutline(),
                          ConstantVec4iAnimation(Vec4i(255, 0, 0, 255)))),
                  None<InitialSymbolSides>(),
                  0,
                  100),
              InitialSymbol(
                  InitialSymbolGlyph(
                      SymbolId("AthSymbols", 120),
                      ConstantVec4iAnimation(Vec4i(255, 255, 255, 380))),
                  Some(
                      InitialSymbolOutline(
                          CenteredOutline(),
                          ConstantVec4iAnimation(Vec4i(0, 0, 0, 255)))),
                  None<InitialSymbolSides>(),
                  0,
                  100),
              List<(i64, InitialSymbol)>(),
              unit.hp,
              unit.maxHp)
          });

  self = UnitPresenter(domino, unit, unitViewId);

  ret self;
}

func OnHop(self &!UnitPresenter, server &!EditorInstance, newLoc Location, newElevation int) {
  self.domino!.UnitHopTo(self.unitViewId, newLoc, newElevation, 300);
  set server.animationsEndTimeFromNow =
      max(server.animationsEndTimeFromNow, 300);
}

func OnUnitDeath(self UnitPresenter, server &!EditorInstance) {
  self.domino!.DestroyUnit(self.unitViewId);
  set server.animationsEndTimeFromNow =
      max(server.animationsEndTimeFromNow, 500);
}

func OnAttack(self &!UnitPresenter, server &!EditorInstance, enemy &Unit) {
  self.domino!.UnitLunge(self.unitViewId, enemy.location, 150);
  set server.animationsEndTimeFromNow =
      max(server.animationsEndTimeFromNow, 150);
}
