
struct Unit {
  id int; // maybe we wont need this someday?
  location! Location;
  elevation! int;

  isPlayer bool; // replace with components

  maybeDestination! Opt<Location>;
  nextActionTime! i64;

  hp! int;
  maxHp int;
}

func SetDestination(self &!Unit, destination Location) {
  set self.maybeDestination = Some(destination);
}

func HopTo(self &!Unit, server &!EditorInstance, newLoc Location, newElevation int) {
  server.locToUnit!.remove(self.location);
  server.unoccupiedWalkableLocs!.add(self.location);
  set self.location = newLoc;
  set self.elevation = newElevation;
  set self.nextActionTime = self.nextActionTime + 600i64;
  (&!server.unitPresenters!.get(self.id)!.get()).OnHop(server, newLoc, newElevation);
  server.unoccupiedWalkableLocs!.remove(self.location);
  server.locToUnit!.add(self.location, self);

  if self.isPlayer {
    set server.gameTime = self.nextActionTime;
  }
}

func Attack(self &!Unit, server &!EditorInstance, enemy &!Unit) {
  set enemy.hp = enemy.hp - 10;
  set self.nextActionTime = self.nextActionTime + 600i64;
  (&!server.unitPresenters!.get(self.id)!.get()).OnAttack(server, &enemy);
  (&!server.unitPresenters!.get(enemy.id)!.get()).OnHpChanged(server);

  if self.isPlayer {
    set server.gameTime = self.nextActionTime;
  }

  if enemy.hp <= 0 {
    // Exhibit 1
    // server.units!.remove(enemy.id);
    DestroyUnit(server, unlet enemy);
  } else {
    unlet enemy;
  }
}

func Act(self &!Unit, server &!EditorInstance) {
  println("unit {self.id} acting!");

  if server.terrainController.terrain.pattern.IsAdjacent(
      self.location,
      server.player.get().location,
      false) {
    self!.Attack(server, &!server.player!.get());
    ret void();
  }

  path =
      Go(
          &server.terrainController.terrain.pattern,
          self.location,
          server.player.get().location,
          true,
          (a, b, distanceSoFar) => {
            if distanceSoFar > 10000 {
              ret false;
            }
            server.terrainController.terrain.pattern.IsAdjacent(a, b, false) and
            server.terrainController.terrain.tiles.ContainsKey(b) and
            server.terrainController.terrain.tiles.get(b).get().walkable
          });
  maybeNextStepLoc Opt<Location> =
    if path.len() > 0 {
      if server.unoccupiedWalkableLocs.contains(path.get(0)) {
        x Opt<Location> = Some(path.get(0));
        x
      } else {
        // We might get here if the next step contains another unit.
        // do nothing
        x Opt<Location> = None<Location>();
        x
      }
    } else {
      adjacents =
          server.terrainController.terrain.GetAdjacentExistingLocations(
              self.location, false);
      possibleWanderLocs = List<Location>();
      foreach adjacent in adjacents {
        if server.unoccupiedWalkableLocs.contains(adjacent) {
          possibleWanderLocs!.add(adjacent);
        }
      }
      if possibleWanderLocs.len() == 0 {
        // do nothing
        x Opt<Location> = None<Location>();
        x
      } else {
        index = abs(TruncateI64ToI32(server.rand!.Next())) mod possibleWanderLocs.len();
        wanderLoc = possibleWanderLocs.get(index);
        x Opt<Location> = Some(wanderLoc);
        x
      }
    };

  if not maybeNextStepLoc.isEmpty() {
    nextStepLoc = maybeNextStepLoc.get();

    nextStepElevation =
        server.terrainController.terrain.tiles.get(nextStepLoc).get().elevation;

    self!.HopTo(server, nextStepLoc, nextStepElevation);
  } else {
    set self.nextActionTime = self.nextActionTime + 600i64;
  }
}
