import stdlib.*;
import rocketvale.*;
import valejson.*;
import stdlib.stringutils.*;
import atharia.model.*;
import atharia.model.animation.*;
import atharia.network.*;


struct Response {
  builder StringBuilder;
}

fn jsonToNode(s StrSlice) Result<JsonNode, str> {
  iter = ParseIter(s, false);
  node_result = parseJson(&!iter);
  if (node_result.is_err()) {
    ret node_result;
  }
  if (iter.rest != "".slice()) {
    ret Err<JsonNode, str>("Still text at end of input: " + iter.rest);
  }
  ret node_result;
}

struct StartRequest {
  screen_gw int;
  screen_gh int;
}

fn handleStartRequest(domino &!GameToDominoConnection, start_request &StartRequest) Result<[], str> {

  initialTileDescription =
      InitialTile(
          Location(start_request.screen_gw, start_request.screen_gh, 5),
          6,
          ConstantVec4iAnimationRed(), //CalculateTintedFrontColor(membersFrontColors[membersFrontColors.Count - 1].Item2, selected, highlighted),
          ConstantVec4iAnimationBlue()); //membersSideColors[membersSideColors.Count - 1].Item2,
          //CalculateMaybeOverlay(membersOverlays),
          //CalculateMaybeFeature(membersFeatures),
          //membersItems);
  tileViewId = domino!.create_tile(initialTileDescription);

  ret Ok<[], str>([]);
}

fn handleRequest(domino &!GameToDominoConnection, node &JsonObject) Result<[], str> {
  maybe_request_type_str = expect_obj_member_str(node, "request_type");
  if (maybe_request_type_str.is_err()) {
    ret Err<[], str>(maybe_request_type_str.expect_err());
  }
  request_type_str = maybe_request_type_str.expect();

  if (request_type_str == "start") {
    maybe_screen_gw = expect_obj_member_int(node, "screen_gw");
    if (maybe_screen_gw.is_err()) {
      ret Err<[], str>(maybe_screen_gw.expect_err());
    }
    screen_gw = maybe_screen_gw.expect();

    maybe_screen_gh = expect_obj_member_int(node, "screen_gh");
    if (maybe_screen_gh.is_err()) {
      ret Err<[], str>(maybe_screen_gh.expect_err());
    }
    screen_gh = maybe_screen_gh.expect();

    request = StartRequest(screen_gw, screen_gh);
    ret handleStartRequest(domino, &request);
  } else {
    ret Err<[], str>("Unknown request type: " + request_type_str);
  }
}

struct MyRequestHandler {
  counter! int;
}
impl IRequestHandler for MyRequestHandler;
fn handle(self &!MyRequestHandler impl IRequestHandler, request_path str, request_json str) str {
  maybe_request_root_node = jsonToNode(request_json.slice());
  if (maybe_request_root_node.is_err()) {
    ret "Couldn't parse request: " + maybe_request_root_node.expect_err();
  }
  request_root_node = (maybe_request_root_node).expect();

  maybe_request_root_obj = request_root_node.as<JsonObject>();
  if (maybe_request_root_obj.is_err()) {
    ret "Request wasn't json object!";
  }
  request_root_obj = maybe_request_root_obj.expect();

  domino = GameToDominoConnection();
  result = handleRequest(&!domino, &request_root_obj);
  if (result.is_err()) {
    ret "Error: " + result.expect_err();
  }

  response = Response(StringBuilder(List<StrSlice>()));
  domino.messages.each((message){
    response.builder!.print(message.JsonNode().str());
  });
  drop(domino);

  set self.counter = self.counter + 1;

  ret "Response: " + response.builder.assembleStr();
}

fn main() export {
  runServer(&MyRequestHandler(0));
}
